-- Some theorems about operations on non-deterministic values

module nondet-thms where

open import bool
open import bool-thms
open import nat
open import eq
open import nat-thms
open import functions
open import nondet

----------------------------------------------------------------------
-- Theorems about values contained in non-deterministic values:

-- A proof that x is value of the non-deterministic tree y:
-- either it is equal to a deterministic value (ndrefl)
-- or it is somewhere in the tree.
-- If it is in the tree then we need to construct both branches of the tree,
-- and a proof that x is in one of the branches
-- A consequence of this is that any proof that x âˆˆ y contains the path
-- to x in the tree.
--
-- Example:
-- hInCoin : H âˆˆ coin
-- hInCoin = left (Val H) (Val T) ndrefl
--
-- Since H is on the left side of coin, we use the left constructor
-- The branches of the tree are (Val H) and (Val T),
-- and since H is identically equal to H this completes the proof.
data _âˆˆ_ {A : Set} (x : A) : (y : ND A) â†’ Set where
  ndrefl : x âˆˆ (Val x)
  left   : (l : ND A) â†’ (r : ND A) â†’ x âˆˆ l â†’ x âˆˆ (l ?? r)
  right  : (l : ND A) â†’ (r : ND A) â†’ x âˆˆ r â†’ x âˆˆ (l ?? r)

-- A basic inductive lemma that shows that âˆˆ is closed under function
-- application. That is, if x âˆˆ nx, then f x âˆˆ mapND f nx 
--
-- Example:
-- ndCons : ... â†’ xs âˆˆ nxs â†’ (x :: xs) âˆˆ mapND (_::_ x) nxs
âˆˆ-apply : {A B : Set} â†’ (f : A â†’ B) â†’ (x : A) â†’ (nx : ND A)
        â†’ x âˆˆ nx â†’ (f x) âˆˆ (mapND f nx)
âˆˆ-apply f x (Val .x) ndrefl = ndrefl
âˆˆ-apply f x (l ?? r) (left  .l .r k) =
  left  (mapND f l) (mapND f r) (âˆˆ-apply f x l k)
âˆˆ-apply f x (l ?? r) (right .l .r k) =
  right (mapND f l) (mapND f r) (âˆˆ-apply f x r k)

----------------------------------------------------------------------
-- Theorems about 'mapND':

-- Combine two mapND applications into one:
mapND-mapND : âˆ€ {A B C : Set} â†’ (f : B â†’ C) (g : A â†’ B) (xs : ND A)
            â†’ mapND f (mapND g xs) â‰¡ mapND (f âˆ˜ g) xs
mapND-mapND f g (Val x) = refl
mapND-mapND f g (t1 ?? t2)
  rewrite mapND-mapND f g t1 | mapND-mapND f g t2 = refl

----------------------------------------------------------------------
-- Theorems about 'always':

-- Extend validity of a function with a deterministic argument to validity of
-- the corresponding non-deterministic function:
always-mapND : âˆ€ {A : Set} â†’ (p : A â†’ ğ”¹) (xs : ND A)
            â†’ ((y : A) â†’ p y â‰¡ tt)
            â†’ always (mapND p xs) â‰¡ tt
always-mapND _ (Val y) prf = prf y
always-mapND p (t1 ?? t2) prf
  rewrite always-mapND p t1 prf
        | always-mapND p t2 prf = refl

-- Extend validity of a function with a deterministic argument to validity of
-- the corresponding non-deterministic function:
always-with-nd-arg : âˆ€ {A : Set} â†’ (p : A â†’ ND ğ”¹) (xs : ND A)
               â†’ ((y : A) â†’ always (p y) â‰¡ tt)
               â†’ always (with-nd-arg p xs) â‰¡ tt
always-with-nd-arg _ (Val y) prf = prf y
always-with-nd-arg p (t1 ?? t2) prf
  rewrite always-with-nd-arg p t1 prf
        | always-with-nd-arg p t2 prf = refl

-- Extend validity of a deterministic function to validity of
-- corresponding function with non-deterministic result:
always-toND : âˆ€ {A : Set} â†’ (p : A â†’ ğ”¹) (x : A)
          â†’ (p x) â‰¡ tt â†’ always (toND p x) â‰¡ tt
always-toND _ _ p = p

-- Extend validity of a deterministic function to validity of
-- corresponding non-deterministic function:
always-det-to-nd : âˆ€ {A : Set} â†’ (p : A â†’ ğ”¹)
          â†’ ((y : A) â†’ (p y) â‰¡ tt)
          â†’ (xs : ND A) â†’ always (det-to-nd p xs) â‰¡ tt
always-det-to-nd p u xs =
  always-with-nd-arg (toND p) xs (Î» x â†’ always-toND p x (u x))

----------------------------------------------------------------------
-- Theorems about 'satisfy':

-- A theorem like filter-map in functional programming:
satisfy-mapND : âˆ€ {A B : Set} â†’ (f : A â†’ B) (xs : ND A) (p : B â†’ ğ”¹)
             â†’ (mapND f xs) satisfy p â‰¡ xs satisfy (p âˆ˜ f)
satisfy-mapND _ (Val x) _ = refl
satisfy-mapND f (t1 ?? t2) p
 rewrite satisfy-mapND f t1 p
       | satisfy-mapND f t2 p = refl

-- Extend validity of function with deterministic argument to validity of
-- non-deterministic function:
satisfy-with-nd-arg : âˆ€ {A B : Set} â†’ (p : B â†’ ğ”¹) (f : A â†’ ND B) (xs : ND A)
               â†’ ((y : A) â†’ (f y) satisfy p â‰¡ tt)
               â†’ (with-nd-arg f xs) satisfy p â‰¡ tt
satisfy-with-nd-arg _ _ (Val y) prf = prf y
satisfy-with-nd-arg p f (t1 ?? t2) prf
  rewrite satisfy-with-nd-arg p f t1 prf
        | satisfy-with-nd-arg p f t2 prf = refl

----------------------------------------------------------------------
-- Theorems about 'every':

mapNDval : âˆ€ (f : â„• â†’ â„•) (v : â„•) (x : ND â„•) â†’
         every _=â„•_ v x â‰¡ tt â†’ every _=â„•_ (f v) (mapND f x) â‰¡ tt
mapNDval f v (Val x) u rewrite =â„•-to-â‰¡ {v} {x} u | =â„•-refl (f x) = refl
mapNDval f v (t1 ?? t2) u
  rewrite mapNDval f v t1 (&&-fst u)
        | mapNDval f v t2 (&&-snd {every _=â„•_ v t1} {every _=â„•_ v t2} u) = refl

----------------------------------------------------------------------
-- This theorms allows to weaken a predicate which is always satisfied:
weak-always-predicate : âˆ€ {A : Set} â†’ (p p1 : A â†’ ğ”¹) (xs : ND A)
                â†’ xs satisfy p â‰¡ tt
                â†’ xs satisfy (Î» x â†’ p1 x || p x) â‰¡ tt
weak-always-predicate p p1 (Val x) u rewrite u | ||-tt (p1 x) = refl
weak-always-predicate p p1 (t1 ?? t2) u
  rewrite weak-always-predicate p p1 t1 (&&-fst u)
        | weak-always-predicate p p1 t2 (&&-snd {t1 satisfy p} {t2 satisfy p} u)
  = refl

----------------------------------------------------------------------
